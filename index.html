<!DOCTYPE html>
<html>
<head>
	<style>
		body canvas {
			margin: 0;
			padding: 0;
			position: absolute;
		}
	</style>
	<title>Shadow Mapping Tutorial</title>
	<script id="no-shadow-vertex-shader" type="x-shader/x-vertex">
		precision mediump float;

		uniform mat4 mProj;
		uniform mat4 mView;
		uniform mat4 mWorld;

		attribute vec3 vPos;
		attribute vec3 vNorm;

		varying vec3 fPos;
		varying vec3 fNorm;

		void main()
		{
			fPos = (mWorld * vec4(vPos, 1.0)).xyz;
			fNorm = (mWorld * vec4(vNorm, 0.0)).xyz;

			gl_Position = mProj * mView * vec4(fPos, 1.0);
		}		
	</script>

	<script id="no-shadow-fragment-shader" type="x-shader/x-fragment">
		precision mediump float;

		uniform vec3 pointLightPosition;
		uniform vec4 meshColor;

		varying vec3 fPos;
		varying vec3 fNorm;

		void main()
		{
			vec3 toLightNormal = normalize(pointLightPosition - fPos);

			float lightIntensity = 0.6 + 0.4 * max(dot(fNorm, toLightNormal), 0.0);

			gl_FragColor = vec4(meshColor.rgb * lightIntensity, meshColor.a);
		}
	</script>

	<script id="shadow-vertex-shader" type="x-shader/x-vertex">
		precision mediump float;

		uniform mat4 mProj;
		uniform mat4 mView;
		uniform mat4 mWorld;

		attribute vec3 vPos;
		attribute vec3 vNorm;

		varying vec3 fPos;
		varying vec3 fNorm;

		void main()
		{
			fPos = (mWorld * vec4(vPos, 1.0)).xyz;
			fNorm = (mWorld * vec4(vNorm, 0.0)).xyz;

			gl_Position = mProj * mView * vec4(fPos, 1.0);
		}
	</script>

	<script id="shadow-fragment-shader" type="x-shader/x-fragment">
		precision mediump float;

		uniform vec3 pointLightPosition;
		uniform vec4 meshColor;

		uniform samplerCube lightShadowMap;
		uniform vec2 shadowClipNearFar;

		uniform float bias;

		varying vec3 fPos;
		varying vec3 fNorm;

		void main()
		{
			vec3 toLightNormal = normalize(pointLightPosition - fPos);

			float fromLightToFrag =
				(length(fPos - pointLightPosition) - shadowClipNearFar.x)
				/
				(shadowClipNearFar.y - shadowClipNearFar.x);

			float shadowMapValue = textureCube(lightShadowMap, -toLightNormal).r;

			float lightIntensity = 0.6;
			if ((shadowMapValue + bias) >= fromLightToFrag) {
				lightIntensity += 0.4 * max(dot(fNorm, toLightNormal), 0.0);
			}

			gl_FragColor = vec4(meshColor.rgb * lightIntensity, meshColor.a);
		}
	</script>

	<script id="mapgen-fragment-shader" type="x-shader/x-fragment">
		precision mediump float;

		uniform vec3 pointLightPosition;
		uniform vec2 shadowClipNearFar;

		varying vec3 fPos;

		void main()
		{
			vec3 fromLightToFrag = (fPos - pointLightPosition);

			float lightFragDist =
				(length(fromLightToFrag) - shadowClipNearFar.x)
				/
				(shadowClipNearFar.y - shadowClipNearFar.x);

			gl_FragColor = vec4(lightFragDist, lightFragDist, lightFragDist, 1.0);
		}
	</script>

	<script id="mapgen-vertex-shader" type="x-shader/x-vertex">
		precision mediump float;

		uniform mat4 mProj;
		uniform mat4 mView;
		uniform mat4 mWorld;

		attribute vec3 vPos;

		varying vec3 fPos;

		void main()
		{
			fPos = (mWorld * vec4(vPos, 1.0)).xyz;

			gl_Position = mProj * mView * vec4(fPos, 1.0);
		}
	</script>
</head>
<body onload="Init();">
<canvas id="gl-surface">
	Get a <a href="http://chrome.google.com">less stupid</a> browser
</canvas>
<script src="libs/async.min.js"></script>
<script src="libs/gl-matrix-min.js"></script>

<script src="app.js"></script>
<script src="util.js"></script>
<script src="Models.js"></script>
<script src="shapes.js"></script>
<script src="LightMapDemoScene.js"></script>
</body>
</html>